5 CLS:ONERRORGOTO51020:POKE&HFBB1,0:ONSTOPGOSUB8:STOPON:GOTO10
8 STOPON:RETURN
10 DEFINT A-Z:DEFSNG Z:T=RND(-TIME)
15 CA=6+RND(1)*3:H=17:T=0:PG=-1:PA=4
20 DIMLO$(16),DIR(16,5),OBJ$(18),DF(18),PO(18),SX(18),AG(18),AT(18),VE(18),PE(18),FV(18),AI$(2),AD$(2),AC$(10),FP(30),DS$(1),PL$(1)
30 RESTORE40000:FORN=0TO16:READLO$(N):FORK=0TO5:READDIR(N,K):NEXTK:NEXTN
40 FORN=0TO10:READAC$(N):NEXTN
50 FORN=0TO18:READOBJ$(N),DF(N),PO(N),SX(N),AG(N),AT(N),VE(N),PE(N),FV(N):IF PO(N)=-1THENPO(N)=RND(N)*14+.5
55 NEXTN
60 AD$(0)="O ":AD$(1)="A ":AI$(0)="UM ":AI$(1)="UMA "
65 VO$="AEIOU":CO$="RTPSFKLCBN":GOSUB11000:DM$=N$:GOSUB11000:CH$=N$
70 ZO$="NAO SE LUTA CONTRA OBJETOS"
200 RESTORE:GOSUB20000
250 PO=PO(0)
300 GOSUB21000
400 XX=56:YY=85:ES=9:NE=8:GOSUB22000
410 AC=AA
500 ON AC+1 GOTO1600,1700,2400,1400,2800,2000,2600,3000,5000
1400 IFMO>1THENGOSUB23050
1405 IFOM=0THENE$="NADA PARA PEGAR":GOSUB22500:GOTO400
1410 XX=240:YY=85:ES=9:NE=OM-1:GOSUB22000
1450 GOSUB23600
1460 IFAG(N)>0THENE$="VOCE NAO CARREGA INIMIGOS !!!":GOSUB22500:GOTO400ELSEIFCA<PE(N)THENE$="EXCESSO DE CARGA !!":GOSUB22500:GOTO400ELSECA=CA-PE(N):FP(N)=-1:PO(N)=-1:GOTO5000
1600 MO=MO+1:IFMO>3THENMO=1
1610 ONMOGOSUB23050,23000,23100
1620 GOTO400
1700 FORNE=0TO3:IFDIR(PO,NE)<>-1THENNEXTNE
1710 NE=NE-1:XX=183:YY=14:ES=14:GOSUB22000
1715 IFPO=14ANDDIR(PO,AA)=15THENYY=100:XX=30:GOSUB12900:PRINT#1,"A PASSAGEM ESTA' ENCANTADA.":PRINT#1,"VOCE SO' PODE SUBIR COM A":PRINT#1,"PALAVRA QUE O FEITICEIRO SABE.":GOTO5000
1720 FORN=1TO18:IFPO(N)=POANDAG(N)>0ANDRND(50)*50<VE(N)+AG(N)THENYY=130:XX=20:GOSUB12900:PRINT#1,AD$(SX(N));OBJ$(N);" IMPEDE SUA":PRINT#1,"PASSAGEM. TENTE DE NOVO.":GOTO5000ELSENEXTN
1750 PO=DIR(PO,AA):PO(0)=PO:GOTO5000
2000 IFMO<>2THENGOSUB23000
2005 IFOM=0THENE$="NADA PARA USAR":GOSUB22500:GOTO400
2010 XX=240:YY=85:ES=9:NE=OM-1:GOSUB22000
2050 FORN=1TO18:IFFP(N)THENAA=AA-1:IFAA=-1THEN2060
2055 NEXTN
2060 IFN<>12THEN2080ELSER=RND(3)*3:YY=30:XX=20:GOSUB12900:PSET(0,32),1:COLOR15:PRINT#1,"VOCE RECUPEROU";R;"PTOS. RESIST."
2065 DF(0)=DF(0)+R:IFDF(0)>9THENPSET(0,41),1:COLOR15:PRINT#1,"E ESTA' COM SAUDE PERFEITA":DF(0)=10
2070 GOTO5000
2080 IFN<>8THEN2120ELSEGOSUB13000:IFN=0THEN5000ELSEYY=151:XX=28:GOSUB12900:PSET(2,153),1:COLOR15
2085 IFVE(N)=0THENPRINT#1,AD$(SX(N));OBJ$(N);" CRIA VIDA ...":DF(N)=10:AT(N)=RND(3)*4+2*AT(N)+2:VE(N)=RND(4)*4+6:FV(N)=RND(7)*7:AG(N)=-8:GOTO2100
2087 PRINT#1,"RESISTENCIA MENTAL :";FV(N):PSET(2,162),1:COLOR15
2090 IFRND(10)*10<FV(N)THENPRINT#1,AD$(SX(N));OBJ$(N);" RESISTE...":GOTO5000ELSEPRINT#1,"VOCE HIPNOTIZA ";AD$(SX(N));OBJ$(N)
2095 AG(N)=-ABS(AG(N)):IFN=4THENPSET(2,171),1:COLOR15:PRINT#1,"ELE DIZ: ";CH$
2100 FP(8)=0:CA=CA+2:DG=DG+50:GOTO5000
2120 IFN<>13THEN2140ELSEGOSUB13000:IFN=0THEN5000ELSEYY=101:XX=10:GOSUB12900:PSET(2,103),1:COLOR11
2125 FP(13)=0:PE(N)=0:PRINT#1,AD$(SX(N));OBJ$(N);" FICOU SEM PESO":GOTO5000
2140 IFN<>18THEN2160ELSEYY=120:XX=20:GOSUB12900:PSET(2,122):PRINT#1,"NO LIVRO ESTA' ESCRITO:CONVERTA":PRINT#1,"O DEMONIO COM '";DM$;"'":GOTO5000
2160 IFN<>17THEN2180ELSEGOSUB13000:IFN=0THEN5000ELSEIF AG(N)=0THENE$=ZO$:GOSUB22500:GOTO400
2165 ZD=DF(N):K=N:N=17:AT(N)=30:GOSUB12400:FP(17)=0:CA=CA+3:GOTO5000
2180 IFN<>15THEN2220
2185 YY=I+20:XX=20:GOSUB12900:IFPO(16)<>POTHEN2200ELSEIFRND(3)<.6THENPRINT#1,"NAO DEU CERTO. BOMBA ATIVA !!!" ELSEPRINT#1,"PARABENS !! BOMBA DESARMADA !":PLAY"O4A-AEFE-EE","O3BCA-AAB-B-","O2FEDCBB-B-":DG=DG+50:PO(16)=-1:TB=0
2190 GOTO5000
2200 IFFP(16)THENPRINT#1,"LARGUE A BOMBA ANTES !!"ELSEPRINT#1,"NAO HA' BOMBA AQUI."
2210 GOTO5000
2220 IFN<>11THEN2390
2225 FORNE=0TO3:IFDIR(PO,NE)<>-1THENNEXTNE
2230 NE=NE-1:XX=183:YY=14:ES=14:GOSUB22000
2235 PO=DIR(PO,AA):YY=40:XX=150:GOSUB12900:PRINT#1,"N";AD$(DIR(PO,5));LO$(PO):PRINT#1,"VOCE ENXERGA :":GOSUB23050:PO=PO(0):GOSUB12800:GOTO5000
2390 YY=150:XX=20:GOSUB12900:PRINT#1,"VOCE NAO PODE 'USAR'":PRINT#1,AI$(SX(N))+OBJ$(N):GOTO5000
2400 IFMO>1THENGOSUB23050
2405 IFOM=0THENE$="NINGUEM COM QUEM LUTAR":GOSUB22500:GOTO400
2410 XX=240:YY=85:ES=9:NE=OM-1:GOSUB22000
2450 GOSUB23600
2460 IFAG(N)=0THENE$=ZO$:GOSUB22500:GOTO400
2500 K=N:N=0:GOSUB12400:GOTO5000
2600 IFMO<>2THENGOSUB23000
2605 IFOM=0THENE$="NADA PARA SOLTAR":GOSUB22500:GOTO400
2610 XX=240:YY=85:ES=9:NE=OM-1:GOSUB22000
2650 FORN=1TO18:IFFP(N)THEN AA=AA-1:IFAA=-1THEN2660
2655 NEXTN
2660 IFN=16ANDRND(3)*5>4THENYY=90:XX=19:GOSUB12900:PRINT#1,"A BOMBA ENGANCHOU NO SEU CINTO. TENTE  DE NOVO...":GOTO5000
2670 PO(N)=PO:FP(N)=0:CA=CA+PE(N):GOTO5000
2800 IFINKEY$<>""THEN2800ELSEPUTSPRITE 8,,0:YY=120:XX=20:GOSUB12900:PRINT#1,"ESCREVA A PALAVRA.":PSET(2,129),1:COLOR14:PRINT#1,"_";:I=0:N$=""
2810 A$=INKEY$:IFA$<>""THEN2820ELSEIFSTRIG(0)ORSTRIG(1)ORI>10THEN2830ELSE2810
2820 IFASC(A$)>64ANDASC(A$)<91THENPSET(2+8*I,129),1:COLOR1:PRINT#1,"_";:PSET(2+8*I,129),1:COLOR14:PRINT#1,A$;"_":I=I+1:N$=N$+A$:SLEEP 500:GOTO2810
2823 IFASC(A$)=8ANDI>0THENA$=RIGHT$(N$,1):N$=LEFT$(N$,I-1):PSET(2+8*I,129),1:COLOR 1:PRINT#1,"_";:I=I-1:PSET(2+8*I,129),1:PRINT#1,A$;:PSET(2+8*I,129),1:COLOR14:PRINT#1,"_";:GOTO2810ELSEPLAY"A","A#":GOTO2810
2825 GOTO2810
2830 YY=90:XX=50:GOSUB12900:LINE(20,120)-(30,110),15:LINE -(20,110):LINE -(20,95):LINE -(120,95):LINE -(120,110):LINE -(40,110):LINE -(20,120)
2840 PSET(22,97),1:COLOR15:PRINT#1,N$;"!":PSET(2,130),1:COLOR14
2850 IFN$=CH$ANDPO=14THEN PRINT#1,"VOCE VAI PARA O TETO...":PO=15:PO(0)=15:GOTO2950
2860 IFN$=DM$ANDBDTHENPRINT#1,"VOCE CONVERTE O DEMONIO...":AG(14)=-AG(14):GOTO2950
2870 PRINT#1,"NADA ACONTECE..."
2950 GOTO5000
3000 YY=20:XX=70:GOSUB 12900:PRINT#1,"INSTRUCOES. ESCOLHA O ASSUNTO":PRINT#1,:PRINT#1,"        MAPA":PRINT#1,"      REGRAS":PRINT#1,"     OBJETOS":PRINT#1," PERSONAGENS":PRINT#1,"   MAIS NADA":XX=95:YY=34:NE=4:ES=8:GOSUB22000
3010 PUTSPRITE 8,,0:ON AA+1 GOTO3020,3100,3150,3200,300
3020 YY=20:XX=150:GOSUB12900
3030 PSET(3,92),1:COLOR15:PRINT#1,"ALTA PAUL.    MASP   BAIXA PAUL."
3035 PSET(47,102):PRINT#1,"RUA":PSET(47,110),1:COLOR15:PRINT#1,"AUGUSTA     BAIXA":PSET(180,102):PRINT#1,"COLEGIO":PSET(144,120),1:COLOR15:PRINT#1,"9 JULHO":PSET(50,140),1:COLOR15:PRINT#1,"AL.SANTOS"
3040 PSET(2,130),1:COLOR15:PRINT#1,"CINE":PSET(2,139),1:COLOR15:PRINT#1,"SESC"
3050 LINE(1,90)-(256,90),15:LINE(1,100)-(256,100),15:LINE(35,100)-(35,165),15:LINE(45,100)-(45,165),15:LINE(47,138)-(200,138),15:LINE(47,148)-(200,148),15:LINE(132,104)-(132,165),15:LINE(142,104)-(142,165):CIRCLE (137,108),7,,0,3.14:CIRCLE(137,82),7,,3.1
3052 LINE(132,86)-(132,20),15:LINE(142,86)-(142,20),15:LINE(200,90)-(200,20):LINE(210,90)-(210,20):LINE(142,30)-(200,30):LINE(142,40)-(200,40):LINE(200,110)-(200,165):LINE(210,110)-(210,165)
3055 PSET(144,55):PRINT#1,"ALTA":PSET(144,63):PRINT#1,"9 JULHO":PSET(212,55):PRINT#1,"BRIGA":PSET(212,63):PRINT#1,"DEIRO":PSET(144,31):PRINT#1,"13 MAIO":PSET(152,42):PRINT#1,"LIVR."
3057 COLOR 15:LINE(45,22)-(45,90):LINE(35,22)-(35,90)
3060 PSET(47,66):PRINT#1,"CENTER":PSET(47,75):PRINT#1,"TRES"
3065 COLOR 1:LINE(200,139)-(200,147):LINE(142,31)-(142,39):LINE(200,31)-(200,39):LINE(132,139)-(132,147):LINE(142,139)-(142,147):LINE(133,138)-(141,138):LINE(133,148)-(141,148):LINE(45,139)-(45,147)
3067 LINE(36,90)-(44,90):LINE(36,100)-(44,100):LINE(201,90)-(209,90):LINE(201,100)-(209,100)
3099 GOSUB12800:GOTO3000
3100 YY=20:XX=150:GOSUB 12900:PRINT#1," O OBJETIVO DO JOGO E' DESTRUIR":PRINT#1," A BRUXA QUE ESTA' NO TETO DO":PRINT#1," MASP ANTES QUE ELA TENHA TEMPO":PRINT#1," DE DIZER AS QUATRO PALAVRAS DO":PRINT#1," FEITICO.":PRINT#1,
3102 PRINT#1," A PASSAGEM PARA O TETO DO MASP":PRINT#1," ESTA' ENCANTADA E VOCE SO' PODE":PRINT#1," CHEGAR LA' DE DUAS MANEIRAS :":PRINT#1," - VOANDO   OU":PRINT#1," - FALANDO A PALAVRA SECRETA":PRINT#1,:PRINT#1,
3105 PRINT#1,"(SEMPRE QUE O 'APERTE' PISCAR":PRINT#1,"VOCE DEVE APERTAR A BARRA DE":PRINT#1,"ESPACOS OU O BOTAO DO JOYSTICK )"
3107 GOSUB 12800:YY=20:XX=150:GOSUB12900
3110 PRINT#1," PARA VOAR ATE' O TETO VOCE":PRINT#1," PRECISA ENCONTRAR A ASA DELTA":PRINT#1," E PULAR DO ALTO DA ANTENA QUE":PRINT#1," FICA EM CIMA DO COLEGIO."
3112 PRINT#1,:PRINT#1," PARA SABER A PALAVRA VOCE TEM":PRINT#1," QUE HIPNOTIZAR O FEITICEIRO":PRINT#1," COM O HIPNODISCO, IR ATE' O":PRINT#1," MASP E FALAR A PALAVRA."
3140 GOSUB12800:GOTO3000
3150 YY=20:XX=150:GOSUB12900
3152 PRINT#1,"EXISTEM VARIOS OBJETOS ESPALHA-":PRINT#1," DOS PELA CIDADE. VOCE PODE :":PRINT#1," PEGAR, SOLTAR OU USAR ESSES":PRINT#1," OBJETOS. A UNICA LIMITACAO E':":PRINT#1," 'VOCE NAO PODE CAREGAR MAIS DO"
3155 PRINT#1," QUE PERMITE A SUA CARGA'":PRINT#1,:PRINT#1," UM OBJETO, PARA SER USADO,DEVE":PRINT#1," ESTAR SENDO CAREGADO POR VOCE.":PRINT#1," ALGUNS OBJETOS SAO APENAS":PRINT#1," USADOS COMO, POR EXEMPLO, O":PRINT#1," KIT SAUDE."
3160 GOSUB12800:XX=150:YY=20:GOSUB12900
3162 PRINT#1," OUTROS SAO USADOS EM OUTROS":PRINT#1," OBJETOS OU PERSONAGENS COMO,":PRINT#1," POR EXEMPLO, A SETA MORTAL E A":PRINT#1," CERA.":PRINT#1,:PRINT#1," PARA SABER O QUE VOCE ESTA'":PRINT#1," CARREGANDO USE A OPCAO 'INFO'."
3165 PRINT#1," USANDO 'INFO' UMA SEGUNDA VEZ,":PRINT#1," VOCE FICA SABENDO COMO VAI SUA":PRINT#1," CAPACIDADE DE CARGA."
3190 GOSUB12800:GOTO3000
3200 YY=20:XX=150:GOSUB12900
3202 PRINT#1," ABAIXO,A LISTA DE QUEM-E'-QUEM":PRINT#1," NO JOGO :":PRINT#1,:PRINT#1," A JRUXA E' SEU GRANDE INIMIGO.":PRINT#1," ELA ESTA' PRESA NO TETO DO":PRINT#1," MASP E DEVE SER DESTRUIDA A":PRINT#1," QUALQUER CUSTO.":PRINT#1,
3205 PRINT#1," A AGUIA E' EXTREMAMENTE AGIL E":PRINT#1," VOA DE LUGAR PARA LUGAR.":PRINT#1,:PRINT#1," A CORUJA E' SEU UNICO ALIADO":PRINT#1," NA CIDADE."
3207 GOSUB12800:YY=20:XX=150:GOSUB12900
3210 PRINT#1," O FEITICEIRO E' UM SERVO DA":PRINT#1," NOITE. ENQUANTO A NOITE NAO":PRINT#1," VEM, ELE TEM PODERES LIMITADOS":PRINT#1," E ESTA' PRESO AO TUNEL DA":PRINT#1," 9 DE JULHO. ELE E' O UNICO QUE":PRINT#1," PODE LHE DIZER A PALAVRA MAGI-"
3212 PRINT#1," CA PARA QUE VOCE SUBA NO MASP.":PRINT#1,
3215 PRINT#1," O BOMBARDEADOR COLOCA BOMBAS":PRINT#1," RELOGIO NOS PREDIOS. PARA":PRINT#1," DESARMAR ESTAS BOMBAS USE O":PRINT#1," KIT BOMBA."
3216 GOSUB12800:YY=20:XX=150:GOSUB12900
3217 PRINT#1," O DEMONIO SO' APARECE NO JOGO":PRINT#1," QUANDO A BRUXA SE SENTE EM":PRINT#1," PERIGO. MAS ELE PODE SE TORNAR":PRINT#1," SEU ALIADO.BASTA QUE VOCE FALE":PRINT#1," A PALAVRA DE CONVERSAO IMEDIA-":PRINT#1," TAMENTE APOS A BRUXA INICIAR A"
3220 PRINT#1," CONVOCACAO. ESTA PALAVRA ESTA'":PRINT#1," ESCRITA NO LIVRO."
3245 GOSUB12800:GOTO3000
5000 GOSUB12800:I=16:PUTSPRITE8,,0
5005 FORN=1TO18:IFPO(N)<0ORVE(N)=0THEN5100ELSEIFPO(N)<>POTHEN5030
5010 FORK=0TO18:IFVE(K)<1ORPO<>PO(K)THEN5020ELSEIF SGN(AG(N)*AG(K))<0ANDRND(5)*10<ABS(AG(N))THENGOSUB12800:GOSUB12400:GOTO5100
5020 NEXTK
5030 IFVE(N)=11OR(RND(4)*20>VE(N)AND(PO<>PO(N)ORDF(N)>RND(9)*4))THEN5050
5037 IFN=2ORN=3THENIFPO(N)<>16THEND=16:GOTO5045ELSED=RND(1)*16:GOTO5045
5040 D=RND(4)*4:FOR K=0TOD:IFDIR(PO(N),K)=-1THEN5040ELSENEXTK:D=DIR(PO(N),D):IFD>14THEN5040
5045 IFPO(N)=POTHENYY=I:XX=19:GOSUB12900:COLOR 11:PRINT#1,AD$(SX(N));OBJ$(N):PRINT#1,"VAI PARA ";AD$(DIR(D,5));LO$(D):I=I+XX+1
5047 PO(N)=D:IFPO=DTHENYY=I:XX=19:GOSUB12900:COLOR11:PRINT#1,AD$(SX(N));OBJ$(N):PRINT#1,"VEM PARA ";AD$(DIR(D,5));LO$(D):I=I+XX+1
5049 GOTO5100
5050 '
5100 NEXTN
6000 FORN=0TO18:IFVE(N)>0ANDDF(N)<10ANDRND(5)*6+5>DF(N)ANDRND(4)*4<1THENYY=I:XX=19:GOSUB12900:PRINT#1,AD$(SX(N));OBJ$(N);" RECUPERA":PRINT#1,"UM PONTO DE RESISTENCIA":DF(N)=DF(N)+1:I=I+XX+1
6010 NEXTN
6030 IFVE(5)>0THENIFDIR(PO(5),4)ORRND(7)*7>1ORTB>0THEN6040ELSETB=RND(3)*9+13:YY=I:XX=28:GOSUB12900:PRINT#1,"O ";OBJ$(5);" ARMOU UMA BOMBA"
6033 PRINT#1,"N";AD$(DIR(PO(5),5));LO$(PO(5));" QUE VAI":PRINT#1,"EXPLODIR EM";TB;" MINUTOS":PB=PO(5):PO(16)=PB:GOTO 6050:I=I+XX+1
6040 IFFP(16)THENPB=PO
6043 IFTB=0THEN6050ELSEIFTB>1THENTB=TB-1:IFTB>0THENYY=I:XX=19:GOSUB12900:PRINT#1,"A BOMBA D";AD$(DIR(PB,5));LO$(PB):PRINT#1,"VAI EXPLODIR EM"TB"MINUTO(S)":I=I+XX+1:GOTO6050
6045 GOSUB12800:I=40:YY=I:XX=150:GOSUB12900:PRINT#1,AD$(DIR(PB,5));LO$(PB);" EXPLODE !!":PRINT#1,"VITIMAS:":PO=PB:GOSUB23050:PO=PO(0):DP=DP+50
6047 SOUND 7,0:SOUND 6,31:PLAY"S0M20000O1C1","T50S0M20000C#1":DIR(PB,4)=-2:RU=RU+1:LO$(PB)="RUINA"+STR$(RU):DIR(PB,5)=1:FORN=1TO18:IFPO(N)=PBTHENPO(N)=-1:VE(N)=0
6049 NEXTN:IFPB=14THENPB=15:GOTO6045ELSEIFPB=15THENPSET(2,180),1:COLOR15:PRINT#1,"... E O CEREBRO !!!":CE=-1:GOTO13500ELSEIFPB=POTHEN13500ELSEPB=0:TB=0
6050 SOUND 7,56
6060 IFTG>0THENTG=TG-1:IFTG=0THENPG=-1
6070 IFPO<>PGTHEN6080ELSEYY=I:XX=20:GOSUB12900:PRINT#1,"AQUI ESTA' CHEIO DE GAS !":IF FP(10) THEN PRINT#1,"SUA MASCARA O PROTEGE":GOTO6078 ELSE R=RND(3)*2+1:DF(0)=DF(0)-R:IFDF(0)<1THENPRINT#1,"VOCE MORRE SUFOCADO.":GOTO13500
6075 PRINT#1,"VOCE PERDE"R"PTOS. RESIST."
6078 I=I+XX+1
6080 IFVE(5)>0ANDTG=0ANDRND(20)*2<.1THENYY=I:XX=20:GOSUB12900:PG=PO(5):TG=RND(0)*6+10:PRINT#1,"O ";OBJ$(5);" SOLTA UMA":PRINT#1,"BOMBA DE GAS N";AD$(DIR(PG,5));LO$(PG)
6085 I=I+XX+1
6100 IFINT(RND(9)*25)<>10THEN6110ELSEYY=I:XX=40:GOSUB12900:PRINT#1,"VOCE OUVE A BRUXA DIZER :":GOSUB11000:PRINT#1,"'";N$;"'."
6105 PA=PA-1:IFPA=0THENPRINT#1,"O CEREBRO VIRA":PRINT#1,"UM BODE...":GOTO 13500 ELSE PRINT#1,"AINDA FALTAM"PA"PALAVRAS":PRINT#1,"PARA QUE O CEREBRO VIRE UM BODE.":I=I+XX+1
6110 IFCA+DF(0)>RND(5)*5THEN6130ELSEGOSUB24000:IFAA<0THEN6130ELSEYY=I:XX=30:GOSUB12900:PRINT#1,"VOCE ESTA' MUITO FRACO PARA":PRINT#1,"CARREGAR TANTA COISA."
6115 PRINT#1,AD$(SX(AA));OBJ$(AA);" CAI DE SUAS MAOS.":I=I+XX+1
6120 PO(AA)=PO:FP(AA)=0:CA=CA+PE(AA)
6130 IFPO(2)=POANDPO=16ANDFP(9)ANDRND(3)*20<1THENYY=I:XX=10:GOSUB12900:PRINT#1,"A AGUIA RASGA SUA ASA DELTA.":FP(9)=0:CA=CA+PE(9):PO(9)=-2:I=I+XX+1
6230 IFVE(4)=11ANDRND(3)*6<H-17 THEN YY=I:XX=10:GOSUB 12900:PRINT#1,"O FEITICEIRO ESTA' SOLTO...":I=I+XX+1:VE(4)=8
6250 IFBDANDPO(1)=POTHENYY=I:XX=20:GOSUB12900:PRINT#1,"APARECE UM DEMONIO...":I=I+XX+1:PO(14)=PO(1):BD=0:IF AG(14)<0 THEN PRINT#1,"ELE ESTA' A SEU FAVOR."
6300 IFPO(14)=-2ANDPO(1)=POANDRND(5)*5<1THENYY=I:XX=20:GOSUB12900:PRINT#1,"A BRUXA INICIA A CONVOCACAO DE":PRINT#1,"UM DEMONIO.";:BD=-1:I=I+XX+1
6320 IFH<18ORRND(6)*500>100*(H-18)+TTHEN6350
6322 YY=I:XX=20:GOSUB12900:I=I+XX+1:PRINT#1,"A NOITE COMANDA O FEITICEIRO."
6325 IFVE(4)=0THENPRINT#1,"ELE RESSUCITA.":VE(4)=6:PO(4)=PO:DF(4)=10:AG(4)=8:I=I+XX+1:GOTO6350
6330 IFAG(4)<0THENPRINT#1,"ELE VOLTA A SER CONTRA VOCE.":AG(4)=-AG(4):GOTO6350
6335 IFPO(4)<>POTHENPRINT#1,"ELE VAI ATE' SUA POSICAO":PO(4)=PO:GOTO6350
6340 PRINT#1,"ELE FICA MAIS PODEROSO.":DF(4)=DF(4)+2:AT(4)=AT(4)+2:GOTO6350
6350 '
6400 IFPO<>16THEN6420ELSEFORN=0TO18:IFPO(N)=16ANDN<>3ANDN<>2ANDNOT(N=0ANDFP(9))THENYY=I:XX=20:GOSUB12900:PRINT#1,AD$(SX(N));OBJ$(N);" SE ESPATIFA":PRINT#1,"LA' EMBAIXO":PO(N)=-1:DF(N)=0:VE(0)=0:IFN=0ORN=1THEN13500
6410 NEXTN
6420 '
6900 T=T+RND(3)*2+1:IFT>59THENH=H+1:T=T-60
6990 GOSUB12800:GOTO300
10000 GOSUB12800:GOTO300
11000 N$="":N=RND(3)*2+2:FORI=1TON:X$=MID$(VO$,RND(5)*5+1,1):Y$=MID$(CO$,RND(10)*10+1,1):IF RND(3)>.5THENN$=N$+X$+Y$ELSEN$=N$+Y$+X$
11005 NEXTI:RETURN
12400 PLAY"T70L16V13","T70L16S0M2000","T70L16S0M2000"
12410 PLAY"O4GAGAB-AB-O5CDCDGDGO6","O4E-E-E-E-DDDO3FAFDBDB","O3CE-CE-O2GE-GFCFGDFG"
12420 LINE(0,20)-(103,132),4,BF:LINE(144,20)-(255,133),4,BF:LINE(4,24)-(251,36),15,BF:LINE(100,20)-(147,39),4,BF:LINE(4,40)-(99,129),1,BF:LINE(148,40)-(251,129),1,BF:LINE(104,24)-(143,35),2,BF
12430 PSET(106,26),2:COLOR1:PRINT#1,"ATACA"
12460 PUTSPRITE8,,0
12470 PSET(8,26),15:COLOR4:PRINT#1,OBJ$(N):PSET(8,42),1:COLOR 15:PRINT#1,"ATAQUE";AT(N)
12480 PSET(152,26),15:COLOR4:PRINT#1,OBJ$(K):PSET(152,42),1:COLOR15:PRINT#1,"RESIST";DF(K)
12490 ZA=AT(N):IFN<>0THENGOSUB13100:GOTO12500ELSEI=51:FORN2=0TO18:IFFP(N2)ANDAT(N2)>0THENPSET(8,I),1:COLOR15:PRINT#1,LEFT$(OBJ$(N2),6);" +";AT(N2):I=I+9:ZA=ZA+AT(N2)
12495 NEXTN2
12500 ZD=DF(K):IFK<>0THENGOSUB13200:GOTO12510ELSEI=51:FORN2=0TO18:IFFP(N2)ANDDF(N2)>0ANDVE(N2)<1THENPSET(152,I),1:COLOR 15:PRINT#1,OBJ$(N2);" +";DF(N2):I=I+9:ZD=ZD+DF(N2)
12505 NEXTN2
12510 '
12650 R=(ZA/ZD)*(RND(I)*1.6+.4)+.8:IFR<0THENR=0ELSER=INT(R):IFR>DF(K)THENR=DF(K)
12660 IFK=0THENDP=DP+RELSEIFN=0THENDG=DG+R
12661 IFPLAY(0)THEN12661
12663 IFR=0THENPLAY"O2C4","O3C4","O4C4"ELSEIFAG(N)<0THENPLAY"O4GCGO5CO4GO5CE","O4CECEGCC","O2CCCGEDC"ELSEIFAG(K)<0THENPLAY"O4CCCDE-E-E-E-CCC","O3GGGF#FFE-DCGC","O2CDE-FGG#GFE-DC"
12665 LINE(144,93)-(255,150),14,BF:LINE(150,97)-(249,146),1,BF:PSET(152,99),1:PRINT#1,"PERDEU";R:PSET(152,108),1:PRINT#1,"PTOS. RESIST."
12670 DF(K)=DF(K)-R:IFDF(K)>0THEN12700ELSEGOSUB12800:LINE(144,40)-(255,133),4,BF:PSET(150,75),4:COLOR15:PRINT#1,"MORREU"
12680 IFAG(K)>0THENDG=DG+30:PLAY"O4FFFEFFFFFF","O3CCCBCCACCC","O2FFFGFFGFFF"ELSE DP=DP+30:PLAY"O3C8O2G8O3E-8C8G8E-8C8O2G8O3E-8C8G8E-8","O2G2G2G2","O2C2C2C2"
12685 PO(K)=-1:VE(K)=0:IFK=0ORK=1THEN13500
12700 GOSUB12800:RETURN
12800 LINE(192,182)-(247,191),1,BF:C=1
12810 PSET(193,183),1:COLORC:PRINT#1,"APERTE":SLEEP 100
12820 C=16-C:IFNOT(STRIG(0)ORSTRIG(1))THEN12810
12830 LINE(192,182)-(247,191),15,BF:PLAY"O4A8","O4B8","O5C8":SLEEP 250:RETURN
12900 IFYY+XX>191THENGOSUB12800:I=20:YY=I
12910 LINE(0,YY)-(255,YY+XX),5,BF:LINE(0,YY+1)-(255,YY+XX-1),1,BF:PSET(2,YY+2),1:COLOR15:RETURN
13000 GOSUB23050
13010 IFOM=0THENE$="NAO HA' ONDE USAR":GOSUB22500:N=0:RETURN
13020 XX=240:YY=85:ES=9:NE=OM-1:GOSUB22000:GOSUB23600:RETURN
13100 I=53:GOSUB13195:IFN=1ANDRND(3)*5<1 THEN PRINT#1,"FEITICO DAS":GOSUB 13195:PRINT#1,"CHAMAS  x 2":ZA=ZA*2:RETURN
13110 IFN=1ANDRND(3)*10<1 THEN PRINT#1,"CANTO DA":GOSUB13195:PRINT#1,"MORTE  x 4":ZA=ZA*4:RETURN
13115 IFN=1ANDRND(3)*10<1THENPRINT#1,"FEITICO DE":GOSUB13195:PRINT#1,"VERTIGEM x3":ZA=ZA*3:RETURN
13120 IFN=4ANDRND(3)*5<1THENPRINT#1,"ADAGA":GOSUB13195:PRINT#1,"MENTAL  + 5":ZA=ZA+5:RETURN
13130 IFN=5ANDRND(3)*5<1THENPRINT#1,"BOMBA":GOSUB13195:PRINT#1,"DE LUZ  + 4":ZA=ZA+4:RETURN
13190 RETURN
13195 PSET(8,I),1:COLOR15:I=I+9:RETURN
13200 PSET(152,53),1:COLOR15:IF(K=1ORK=4)ANDRND(3)*4<1THENPRINT#1,"ESCUDO":PSET(152,62),1:COLOR15:PRINT#1,"MISTICO  +6":ZD=ZD+6:RETURN
13290 RETURN
13500 GOSUB12800:COLOR15,1,9:SCREEN 2,1:LINE(40,0)-(190,12),4,BF:PSET(42,2),4:COLOR15:PRINT#1,"CONTAGEM DE PONTOS":PRINT#1,:PRINT#1,:COLOR11:PRINT#1,"PONTOS A FAVOR :   ";DG:PRINT#1,"PONTOS CONTRA  : - ";DP
13505 SOUND7,56
13510 PRINT#1,:PRINT#1,"TOTAL DE PONTOS:  ";DG-DP:IFDG-DP>0THENR=2ELSER=3
13520 PRINT#1,:IFVE(1)=0THENPRINT#1,"BRUXA MORTA !!!":R=1
13530 IFPA=0THENPRINT#1,"CEREBRO VIROU BODE.":R=4
13535 IFCETHENPRINT#1,"CEREBRO EXPLODIDO.":R=4
13580 PRINT#1,:COLOR15:PRINT#1,"RESULTADO FINAL : ":PRINT#1,:PRINT#1,:PRINT#1,"     ";
13585 IFR<3THENPRINT#1,"VITORIA ";ELSEPRINT#1,"DERROTA ";
13587 IFR=1ORR=4THENPRINT#1,"TOTAL."ELSEPRINT#1,"PARCIAL."
13590 A$="O2GGGGFFFFDDDDFFFF":B$="O3B-B-B-B-AAAAFFFFAAAA":C$="O4GAB-O5CDGDGFEDO4B-AB-AB-":D$="T60L32V9":PLAY D$,D$,D$:PLAY A$:PLAY A$,B$:FORN=1TO4:PLAY A$,B$,C$:NEXTN
13595 PLAY"GGGG","GGGG","GGGG"
13600 IFPLAY(1)THEN13600
13900 GOSUB12800:RUN
20000 A$="T60L16":PLAY A$,A$,A$
20005 GOSUB20210
20010 A$="S0M8000O2C2O1B-4O2G4":B$="V9O3E-DE-DE-DE-DE-DE-DE-DE-D":C$="V9O3GFGFGFGFGFGFGFGF"
20020 PLAY A$,B$:PLAY A$,B$,C$
20025 GOSUB30000:GOSUB 20220:C$="V11O4GDGDGDGAB-DAGFDAD"
20027 PLAY A$,B$,C$:PLAY A$,B$,C$
20030 D$="S0M8000O2F2O1B-4O2B-4":E$="V9O3E-FE-FE-FE-FE-FE-FE-FE-F":F$="V11O4B-CFCFCFGB-CGCB-CGF"
20035 GOSUB20300
20040 PLAY D$,E$,F$:PLAY D$,E$,F$
20050 GOSUB20400
20060 PLAY A$,B$,C$:PLAY A$,B$,C$
20070 PLAY "S0M16000O2C1","V9O3C4","V11O4G4"
20080 GOSUB20500
20100 RETURN
20210 SCREEN2,1:COLOR4,9,9:SCREEN2,1:COLOR4,9,9:OPEN"GRP:"FOROUTPUTAS#1
20212 LINE(0,162)-(256,191),1,BF:LINE(20,40)-(60,162),1,BF:LINE(65,120)-(150,162),1,BF:LINE(80,60)-(110,120),1,BF:LINE(135,20)-(150,120),1,BF:LINE(160,50)-(190,162),1,BF:LINE(212,50)-(240,162),1,BF
20214 CIRCLE(180,15),15,15:PAINT(180,15),15:CIRCLE(172,21),15,9:PAINT(172,21),9
20219 SLEEP 100:RETURN
20220 LINE(46,46)-(194,154),6,BF:LINE(50,50)-(190,150),8,BF
20230 LINE(58,58)-(182,142),1,BF
20240 FORPP=170TO60STEP -1:PUTSPRITE1,(PP,60),14,0:SLEEP 10:NEXTPP
20250 FORPP=60TO78:PUTSPRITE0,(PP,60),14,1:SLEEP 10:NEXTPP
20255 LINE(28,78)-(212,135),6,BF:LINE(34,84)-(206,129),1,BF
20290 SLEEP 100:RETURN
20300 FORN=120TO0STEP -3:PUTSPRITE2,(60+N,84),15,2:PUTSPRITE3,(69+N/2,88),15,0
20305 PUTSPRITE 4,(90+N/3,92),15,3:PUTSPRITE 5,(108+N/4,96),15,4
20310 PUTSPRITE 6,(121-N/4,100),15,5:PUTSPRITE7,(130-N/3,104),15,6
20320 PUTSPRITE8,(146-N/2,108),15,7:PUTSPRITE 9,(160-N,112),15,0
20330 SLEEP 100:NEXTN
20370 FORN=2TO9:PUTSPRITE N,,15:SLEEP 10:NEXTN
20390 SLEEP 100:RETURN
20400 FORPP=60TO200:PUTSPRITE1,(PP,60),15,0:SLEEP 5:NEXTPP:PUTSPRITE 1,,0,0:SLEEP 10
20410 FORPP=60TO170:PUTSPRITE0,(78,PP),15,1:SLEEP 5:NEXTPP:PUTSPRITE 0,,0,1:SLEEP 10
20420 SLEEP 100:RETURN
20500 FORN=2TO9:FORK=0TO10:REM 140 TANGA
20510 PUTSPRITE N,,11:SLEEP 0:PUTSPRITE N,,9:SLEEP 0:NEXTK,N
20530 LINE(28,78)-(212,135),6,BF
20540 LINE(46,46)-(194,154),6,BF
20590 SLEEP 500:RETURN
21000 SCREEN2:COLOR 4,9,9
21010 LINE(200,3)-(250,6),4,BF:PUTSPRITE2,(210,0),15,0:PUTSPRITE 1,(230,0),15,2
21020 LINE(200,9)-(250,12),5,BF
21030 LINE(6,1)-(185,14),6,BF:LINE(8,3)-(183,12),1,BF
21040 PSET(10,4),0:COLOR14:PRINT#1,"POSICAO:";LO$(PO):N=0
21050 Y=14+N*14:LINE(16,Y-2)-(16,9+Y),6:LINE -(71,9+Y),6:LINE(71,4+Y)-(183,14+Y),6,BF:PSET(74,6+Y),6:COLOR15:PRINT#1,LO$(DIR(PO,N));:PSET(67,6+Y),0:COLOR6:PRINT#1,"Ã"
21060 N=N+1:IFN<4ANDDIR(PO,N)<>-1THEN21050
21100 LINE(4,85)-(55,176),5,BF:LINE(8,89)-(51,172),1,BF
21120 FORY=91TO163STEP9:PSET(10,Y),1:COLOR15:PRINT#1,AC$((Y-91)/9);:NEXTY
21200 MO=1:GOSUB23050
21300 LINE(200,24)-(251,60),6,BF:LINE(202,26)-(249,48),9,B
21310 PSET(210,28),6:COLOR9:PRINT#1,"HORA";:PSET(207,39),6:COLOR9
21320 PRINT#1,USING"##";H;:PRINT#1,USING":#";T\10;:PRINT#1,USING"#";T MOD 10
21330 IFH>17THENPSET(207,52),9:COLOR1:PRINT#1,"NOITE"
21990 RETURN
22000 AA=0:CO=1:TT=0:I=0:PLAY "T60S0M2000L32","T60S0M2000L32","T60S0M2000L32"
22010 PUTSPRITE8,(XX,YY+ES*AA),CO:IFTT>20THENCO=16-CO:TT=0ELSETT=TT+1
22015 SLEEP 1:IFI>0THENI=I-1:GOTO22010
22020 ST=STICK(0)ORSTICK(1):IFST>0THEN22050
22030 IFNOT(STRIG(0)ORSTRIG(1))THEN22010
22040 PUTSPRITE8,(XX,YY+ES*AA),5
22045 IFSTRIG(0)ORSTRIG(1)THEN22045ELSEPLAY"O3A","O4C","O5A":RETURN
22050 IFST=1THENAA=AA-1:PLAY"O3A"ELSEIFST=5THENPLAY"O4A":AA=AA+1
22060 IFAA<0THENAA=0ELSEIFAA>NETHENAA=NE
22070 I=10:GOTO22010
22500 PLAY "T60V10O2C8","T60V14O3G8","T60V14O4F#8"
22510 LINE(8,183)-(240,191),10,BF:PSET(10,184),10:COLOR1:PRINT#1,E$;:RETURN
23000 LINE(80,85)-(239,180),14,BF:LINE(84,89)-(235,176),1,BF:LINE (80,74)-(167,85),14,BF:LINE (82,76)-(165,86),1,B:PSET(86,78),14:COLOR1:PRINT#1,"VOCE LEVA";
23010 OM=0:COLOR15:FORN=1TO18:IFFP(N)THENPSET(88,OM*9+91),1:PRINT#1,OBJ$(N);:OM=OM+1
23020 NEXTN
23038 RETURN
23050 LINE(80,85)-(239,180),13,BF:LINE(84,89)-(235,176),1,BF:LINE(80,74)-(167,85),13,BF:LINE(82,76)-(165,86),1,B:PSET(86,78),13:COLOR1:PRINT#1,"AQUI HA'";
23060 OM=0:COLOR 15:FOR N=1 TO 18:IF PO(N)=PO THEN PSET(88,OM*9+91),1:PRINT#1,OBJ$(N);:OM=OM+1
23070 NEXTN
23080 RETURN
23100 LINE(80,85)-(239,180),11,BF:LINE(84,89)-(235,176),1,BF:LINE (80,74)-(167,85),11,BF:LINE (82,76)-(165,86),1,B:PSET(84,78),11:COLOR1:PRINT#1,"SEUS DADOS";
23110 PSET(88,100),1:COLOR15:PRINT#1,"CARGA :";CA;:PSET(88,109):PRINT#1,"RESISTENCIA:";DF(0):PSET(88,118):PRINT#1,"PONTOS FAVOR:";DG:PSET(88,127):PRINT#1,"PONTOS CONTRA:";DP
23120 RETURN
23600 FORN=1TO18:IFPO(N)=POTHENAA=AA-1:IFAA=-1THEN23620
23610 NEXTN
23620 RETURN
23700 FORN=1TO18:IFFP(N)THENAA=AA-1:IFAA=-1THEN23720
23710 NEXTN
23720 RETURN
24000 AA=0:FORN=1TO18:IFFP(N)THENAA=AA+1
24010 NEXTN:IFAA=0THENAA=-1:RETURN
24020 AA=RND(AA)*AA:GOSUB 23700:AA=N:RETURN
30000 DATA7,15,31,59,127,227,195,0
30005 DATA195,199,206,220,248,243,227,0
30010 DATA252,254,230,252,248,224,224,0
30015 DATA238,238,238,238,238,254,124,0
30020 DATA224,224,224,224,224,252,124,0
30025 DATA56,56,56,56,56,56,56,0
30030 DATA30,60,120,240,120,60,120,240
30035 DATA254,254,56,56,56,56,56,0
30040 DATA16,48,127,255,127,48,16,0
30075 FORK=0TO8
30080 SP$="":FORN=1TO8:READX:SP$=SP$+CHR$(X):NEXTN:SPRITE$(K)=SP$
30085 NEXTK
31000 RETURN
40000 DATAALTA PAULISTA,1,2,9,14,-1,1,CENTER 3,2,0,-1,0,0,0,R.AUGUSTA,0,1,3,4,-1,1,CINESESC,2,-1,0,0,0,0,AL.SANTOS,2,13,-1,0,-1,1,ALTA 9-JULHO,6,12,14,-1,-1,1
40010 DATAR.13 DE MAIO,5,7,8,-1,-1,1,LIVRARIA,6,-1,0,0,0,1,BRIGADEIRO,6,9,13,-1,-1,1,BAIXA PAULISTA,0,8,10,14,-1,1,COL.OBJETIVO,9,11,-1,0,-1,0,ANTENA,10,16,-1,0,-1,1
40020 DATATUNEL,5,13,-1,0,0,0,BAIXA 9-JULHO,4,12,8,-1,-1,1,MASP,0,5,9,15,0,0,TETO DO MASP,14,-1,0,0,-1,0,CEU DA CIDADE,15,4,2,13,-1,0
40030 DATAINFO,MOVA,LUTE,PEGUE,FALE,USE,SOLTE,INSTR,NADA,,
40050 DATAVOCE,10,-1,2,-1,3,5,5,5,BRUXA,10,15,1,10,5,11,3,10,AGUIA,10,16,1,8,8,10,2,6
40060 DATACORUJA,10,-1,1,-5,3,9,2,10,FEITICEIRO,10,12,0,7,6,11,5,5,BOMBARDEADOR,12,-1,0,3,8,14,5,10,ESPADA,0,-1,1,0,3,0,3,0,ESCUDO,3,-1,0,0,0,0,3,0,HIPNODISCO,0,-1,0,0,0,0,2,0
40070 DATA ASA DELTA,0,-1,1,0,0,0,3,0,MASCARA,0,-1,1,0,0,0,1,0,LUNETA,0,-1,1,0,0,0,1,0
40080 DATAKIT SAUDE,0,-1,0,0,0,0,1,0,CERA,0,-1,1,0,0,0,0,0,DEMONIO,16,-2,0,8,10,5,8,8
40090 DATAKIT BOMBA,0,1,0,0,0,0,2,0,BOMBA,0,-2,1,0,0,0,3,0,SETA MORTAL,0,-1,1,0,0,0,3,0,LIVRO,0,7,0,0,0,0,0,0
50000 SCREEN2:FORN=0TO15:LINE(N*15,0)-(N*15+14,100),N,BF:NEXT
50010 GOTO50010
50100 SCREEN2:COLOR1,1,1:SCREEN2
50110 R=RND(TIME)*16:LINE(0,0)-(256,200),R,BF:GOTO50110
51000 SOUND7,0:SOUND 6,21:PLAY"S0M20000O1C1","T50S0M20000C#1"
51010 GOTO51010
51020 STOPON:FORI=&H8001TO&HCC6A:POKEI,0
