60 REM
70 DIM MM(30), FP(30), VOC$(14), OBJ$(30), LO$(19), D(19, 1), CF%(10), BL(10)
80 FOR N = 0 TO 14: READ VOC$(N): NEXT N: FOR N = 0 TO 30: READ OBJ$(N): NEXT N: FOR N = 0 TO 19: READ LO$(N): NEXT N
90 FOR N = 0 TO 30: MM(N) = INT(RND * 15): NEXT N: FOR N = 15 TO 19: D(N, 0) = -1: D(N, 1) = -1: NEXT N: D(16, 0) = INT(RND * 15): D(16, 1) = INT(RND * 15)
100 FOR N = 24 TO 30: MM(N) = -1: NEXT N: MM(2) = 8: MM(16) = 13: MM(15) = 4: MM(19) = -1: MM(4) = 15: MM(6) = 16: K = INT(RND * 2 + 15): MM(7) = -1: MM(11) = K: MM(3) = 31 - K: MM(13) = -1: MM(17) = -1: MM(27) = 19: MM(7) = 18: MM(28) = 18
110 FOR N = 0 TO 10: READ BL(N): NEXT N
120 VE$ = "VOCE ESTA' ": VO$ = "VOCE OUVE ": TEMPO = 0
130 FOR N = 0 TO 14
140 D(N, 0) = INT(RND * 15): D(N, 1) = INT(RND * 15): IF D(N, 0) = D(N, 1) OR D(N, 0) = N OR D(N, 1) = N THEN 140
141 NEXT N
150 DATA FUJA,LUTE,PEGUE,SAIA,AJUDA,FALE,ABRA,ENTRE,LISTE,TOQUE,DESCANSE,DESISTA,CHEAT,,
160 DATA A ESPADA," ESCUDO"," LIVRO",A CAPA," DIAMANTE"," ANEL"," ESPELHO"," PERGAMINHO",A MOEDA DE OURO,A LANTERNA,A FLAUTA," ELMO",A GARRAFA DE VINHO," CETRO DE PRATA",A CHAVE,A ARCA,A ESTATUA,A PASSAGEM," ALTAR DE AKERNAAK",A ESFERA DOS ELFOS
170 DATA " DUENDE"," OGRO"," TIGRE"," ELFO"," PITON"," MAGO"," GUERREIRO AKERNAAK"," FEITICEIRO DO FOGO"," MESTRE-FEITICEIRO"," ASTERION"," DRAGAO"
180 DATA A ENTRADA DA CASA,O HALL,O CORREDOR,A ESCADA,O QUARTO
190 DATA A GARAGEM,A SALA,O SALAO DE JOGOS,A BIBLIOTECA,A PORTA DOS FUNDOS
200 DATA A DESPENSA,A COZINHA,O BANHEIRO,O GRAMADO,O MURO
210 DATA A ARCA,A PASSAGEM SECRETA,O CASTELO VERMELHO,O DIAMANTE,O CAMPO ZERO
220 DATA 3,6,7,1,6,2,6,3,3,2,3
230  PRINT STRING$(38, "=")
240  PRINT STRING$(10, " ") + "A CASA DE AKERNAAK"
250  PRINT STRING$(38, "=")
260  PRINT "VOCE VOLTOU PARA CASA APOS UM DIA DE":  PRINT "TRABALHO.":  PRINT "AO CHEGAR NA ENTRADA DA CASA,POREM,":  PRINT "VOCE SENTIU QUE TUDO SE TORCIA A":  PRINT "SUA VOLTA.":  PRINT "ERA UMA BARREIRA AKERNAAK..."
270  PRINT "OS ADORADORES DE AKERNAAK TOMARAM":  PRINT "POSSE DE SUA CASA E O ISOLARAM":  PRINT "DO UNIVERSO."
280  PRINT :  PRINT "QUANDO VOCE NAO SOUBER O QUE FAZER,":  PRINT "DIGITE 'AJUDA'"
290 P = 0
300  PRINT VE$; "N"; LO$(P)
310 FE = 0: FOR N = 0 TO 30: IF MM(N) <> P THEN 330 ELSE IF NOT FE THEN PRINT "AQUI HA' :"; : FE = -1
320  PRINT "UM"; OBJ$(N); ", ";
330 NEXT N: IF FE THEN PRINT
340 IF D(P, 0) = -1 OR D(P, 1) = -1 THEN 360
350  PRINT "VOCE PODE SE MOVER PARA "; LO$(D(P, 0)); " OU PARA "; LO$(D(P, 1))
360 IF CF < -3 THEN PRINT VE$; : IF CF < -6 THEN PRINT "FERIDO" ELSE PRINT "CANSADO" ELSE IF CF > 1 THEN PRINT VE$; "SUPER-RESISTENTE"
370 INPUT A: FOR N = 0 TO 14: IF MID$(LO$(N), 3, LEN(A)) = A THEN 440
371 NEXT N
380 FOR N = 0 TO 14: IF VOC$(N) = A THEN GOTO 390
381 NEXT N: GOTO 400
390 ON N + 1 GOTO 1060, 940, 790, 1400, 550, 610, 1230, 1480, 870, 1010, 1140, 2230, 1811
400 PRINT "NAO ENTENDI ISTO ": GOTO 370
410 REM
420 REM ===== MOVIMENTAÄAO =====
430 REM
440 IF FB > 0 THEN 480
450 IF D(P, 0) = N OR D(P, 1) = N THEN P = N ELSE 490
460 IF FS > 0 THEN MM(FS) = P: FS = 0
470 GOTO 1560
480 N = FB:  PRINT "O"; OBJ$(N); " PULA SOBRE VOCE!": GOSUB 1850: GOTO 1560
490 REM
500 PRINT "NAO HA' PASSAGEM PARA "; LO$(N)
510 GOTO 1560
520 REM
530 REM ===== AJUDA
540 REM
550 PRINT "O SEU OBJETIVO E' LIBERTAR SUA CASA DOS":  PRINT "AKERNAAKEN.":  PRINT "PARA SE MOVER USE O NOME DO LOCAL PARA ONDE QUISER IR.EX: 'GARAGEM'."
560 PRINT "SEU VOCABULARIO ê: ": FOR N = 0 TO 11: PRINT VOC$(N); ", "; : NEXT N:  PRINT
565 DELAY = 8: GOSUB 60050
570 GOTO 1560
580 REM
590 REM ===== FALE
600 REM
610 INPUT "O QUE"; A:  PRINT "-"; A; "!!": IF A = "AKON" THEN 660
620 IF VAL(A) <> 0 THEN IF VAL(A) = RN THEN RN = 0: PRINT VE$; "SUPER-RESISTENTE POR ALGUM TEMPO": CF = CF + INT(RND * 4 + 6): GOTO 1560 ELSE PRINT "NAO DIGA QUALQUER NUMERO NUMA CASA DE AKERNAAK!": CF = CF - 8: GOTO 1560
630 IF A = PM$ AND MM(30) = P THEN 2200
640 PRINT "NADA ACONTECE..."
650 GOTO 1560
660 IF INT(RND * 4 + 1) > FA THEN FA = FA + 1: GOTO 750
670 FA = FA + 1: IF INT(RND * 3 + 1) = 2 THEN 720
680 PRINT "A BARREIRA SOFREU UMA SATURAÄAO...": PRINT "O UNIVERSO PODERIA TER SIDO DESTRUIDO": PRINT "COM ESSE USO INDISCRIMINADO DE PALAVRAS": PRINT "MAGICAS. FELIZMENTE SO' VOCE SOFREU"
690 PRINT "A CONSEQUENCIA DE SUA IMPRUDENCIA: ":  PRINT "SURGE UM DOS SETE ASTERIONS..."
700 MM(29) = P: BL(9) = 8
710 GOTO 1560
720 PRINT "A PALAVRA 'AKON' ORIGINOU-SE NA PRIMEIRA":  PRINT "BATALHA ENTRE AKERNAAK E AS FORÄAS DO":  PRINT "BEM CHEFIADAS POR WORIEL. AO PERCEBER QUE":  PRINT "A BATALHA NAO TERMINARIA,WORIEL":  PRINT "PRONUNCIOU-A CINCO VEZES.O CAOS GERADO":  PRINT "DEU ORIGEM AO UNIVERSO"
730 PRINT "DECIDIRAM ENTAO QUE ESTA PALAVRA SO'":  PRINT "SERIA DITA NOVAMENTE NO FIM DOS TEMPOS..."
740 PRINT "VOCE DESTRUIU O UNIVERSO...": GOTO 2270
750 D(P, 0) = INT(RND * 15): D(P, 1) = INT(RND * 15): IF D(P, 0) = D(P, 1) OR D(P, 0) = P OR D(P, 1) = P THEN 750 ELSE PRINT "VOCE CONSEGUIU TORCER A BARREIRA": GOTO 1560
760 REM
770 REM ===== PEGUE
780 REM
790 INPUT "O QUE"; A: FOR N = 0 TO 30: GOSUB 2220: IF EX THEN 820
791 NEXT N
800 GOTO 950
810 GOTO 1560
820 IF N > 14 THEN IF N > 19 THEN PRINT VE$; "DOIDO OU VOCE NAO SABE O QUE ê UM"; OBJ$(N); "?": GOTO 1560 ELSE PRINT "VOCE NAO PODE CARREGAR UM"; OBJ$(N): GOTO 1560
830 FP(N) = -1: MM(N) = -1: GOTO 870
840 REM
850 REM =====LISTE
860 REM
870 FE = 0: FOR N = 0 TO 14: IF NOT FP(N) THEN 890 ELSE IF NOT FE THEN PRINT VE$; "LEVANDO: "; : FE = -1
880 PRINT "UM"; OBJ$(N); ", ";
890 NEXT N: IF FE THEN PRINT  ELSE PRINT "VOCE NAO CARREGA NADA"
895 DELAY = 6: GOSUB 60050
900 GOTO 1560
910 REM
920 REM =====LUTE
930 REM
940 INPUT "COM QUEM"; A: FOR N = 0 TO 30: GOSUB 2220: IF EX THEN 960
941 NEXT N
950 PRINT "NAO HA' "; A; " AQUI": GOTO 1560
960 IF N < 20 THEN PRINT "VOCE QUER LUTAR CONTRA UM"; OBJ$(N); "???": GOTO 1560
970 CB = 2: GOSUB 1850: GOTO 1560
980 REM
990 REM ===== TOQUE
1000 REM
1010 IF NOT FP(10) THEN PRINT "VOCE NAO LEVA NENHUM INSTRUMENTO" ELSE PRINT "VOCE COMEÄA A TOCAR A FLAUTA": FT = -1
1020 GOTO 1560
1030 REM
1040 REM =====FUJA
1050 REM
1060 INPUT "PARA ONDE"; A: FOR N = 0 TO 19: IF MID$(LO$(N), 3, LEN(A)) = A THEN GOTO 1070 ELSE IF N > 14 THEN 1490
1061 NEXT N: PRINT "O QUE ISSO ?": GOTO 1060
1070 IF FB = 0 THEN 450
1080 IF INT(RND * 5 + 1) = 3 THEN CB = -2: N = FB: PRINT "VOCE TROPEÄA E O"; OBJ$(N); " O ATACA PELAS COSTAS": GOSUB 1850: GOTO 1590
1090 IF INT(RND * 3 + 1) < 3 THEN PRINT "O"; OBJ$(FB); " O SEGUE": FS = FB: FB = 0: GOTO 450
1100 FB = 0: GOTO 450
1110 REM
1120 REM ===== DESCANSE
1130 REM
1140 FE = 0:  PRINT "VOCE "; : IF FP(0) THEN FP(0) = 0: MM(0) = P: FE = -1
1150 IF FP(1) THEN FP(1) = 0: MM(1) = P: FE = -1
1160 IF FP(11) THEN FP(11) = 0: MM(11) = P: FE = -1
1170 IF FE THEN PRINT " LARGOU SUAS ARMAS E ";
1180  PRINT "ESTA' DESCANSANDO": CF = CF + 2: IF CF > 0 THEN CF = 0
1190 GOTO 1560
1200 REM
1210 REM ===== ABRA
1220 REM
1230 INPUT "O QUE"; A: FOR N = 0 TO 30: GOSUB 2220: IF EX THEN 1232
1231 NEXT N: GOTO 950
1232 IF N <> 15 THEN 1250 ELSE IF FP(14) THEN PRINT "VOCE ABRIU A ARCA"; : FA% = -1: IF INT(RND * 3 + 1) = 2 THEN PRINT "E FOI SUGADO POR ELA. SUA CHAVE ESTA' FORA.": FA% = 0: P = 15: FP(14) = 0: MM(14) = INT(RND * 1)
1240 FB = 0: GOTO 1560
1250 IF N = 2 THEN IF NOT FP(2) THEN PRINT " O LIVRO"; : GOTO 1290 ELSE 1310
1260 IF N = 12 THEN IF NOT FP(12) THEN PRINT " A GARRAFA"; : GOTO 1290 ELSE FG = -1: PRINT "A GARRAFA ESTA' ABERTA": GOTO 1560
1270 IF N = 7 THEN IF NOT FP(7) THEN PRINT "O PERGAMINHO"; : GOTO 1290 ELSE GOSUB 2280: PM$ = A: GOSUB 2280: PM$ = PM$ + A: PRINT "VOCE LE:": PRINT "- A PALAVRA SECRETA E' "; PM$: PRINT "ELE DESAPARECE...": FP(7) = 0: GOTO 1560
1280  PRINT "VOCE NAO PODE ABRIR UM"; OBJ$(N): GOTO 1560
1290  PRINT "NAO ESTA' COM VOCE": GOTO 1560
1300  PRINT "A ARCA ESTA' TRANCADA": GOTO 1560
1310 FP(2) = 0: MM(2) = INT(RND * 20): PRINT "ANTES QUE O LIVRO DESAPAREÄA DE SUAS MAOS,VOCE LE:": ON INT(RND * 5 + 1) GOTO 1320, 1330, 1340, 1350, 1360
1320 RN = INT(RND * 100 + 1):   PRINT "-O NUMERO MAGICO ê :"; RN: GOTO 1560
1330  PRINT "-...PORQUE OS MAGOS DETESTAM VINHO.": GOTO 1560
1340 D(15, 0) = INT(RND * 15):   PRINT "HA' UMA PASSAGEM DA ARCA PARA "; LO$(D(15, 0)): GOTO 1560
1350  PRINT "-'AKON' ê A PALAVRA DAS DIREÄOES": GOTO 1560
1360  PRINT "-...O PERGAMINHO QUE ESTA' DENTRO DO DIAMANTE TEM O SEGREDO": GOTO 1560
1370 REM
1380 REM =====SAIA
1390 REM
1400 IF P <> 15 THEN 1420 ELSE IF INT(RND * 3 + 1) = 2 THEN MM(15) = 19: PRINT "ERA UMA EMBOSCADA..." ELSE IF INT(RND * 5 + 1) < 3 THEN MM(15) = INT(RND * 15): PRINT "A ARCA SE MOVEU"
1410 FB = 0: P = MM(15): GOTO 1560
1420 IF P = 18 THEN P = MM(4): FB = 0: GOTO 1560
1430 REM
1440  PRINT "O QUE VOCE QUER DIZER COM 'SAIR'?": GOTO 1560
1450 REM
1460 REM =====ENTRE
1470 REM
1480 INPUT "ONDE"; A: FOR N = 0 TO 19: IF A = MID$(LO$(N), 3, LEN(A)) THEN GOTO 1490
1481 NEXT N: GOTO 950
1490 IF N < 15 THEN 440 ELSE IF N = 15 THEN IF FA% THEN FB = 0: P = N: GOTO 1560 ELSE 1300
1500 IF N = 18 THEN FP(4) = 0: MM(4) = P: FB = 0: P = N: GOTO 1560
1510 IF N = 16 THEN FB = 0: P = N: GOTO 1560
1520  PRINT "VOCE NAO PODE ENTRAR N"; LO$(N): GOTO 1560
1530 REM
1540 REM =====ACONTECIMENTOS POSSIVEIS=====
1550 REM
1560 DELAY = 4: GOSUB 60050: CLS : FOR N = 20 TO 30: IF MM(N) <> P THEN 1580 ELSE IF INT(RND * 10 + 1) > BL(N - 20) THEN 1580
1570  PRINT "O"; OBJ$(N); " O ATACA !": CB = 0: GOSUB 1850
1580 NEXT N
1590 FOR N = 20 TO 30: IF FB > 0 OR INT(RND * 6 + 1) < 4 OR MM(N) < 0 THEN 1620 ELSE K = MM(N): MM(N) = D(MM(N), INT(RND * 2)): IF MM(N) = -1 THEN MM(N) = K: GOTO 1620
1600 IF P = K THEN PRINT "O"; OBJ$(N); " VAI PARA "; LO$(MM(N))
1610 IF MM(N) = P THEN PRINT "UM"; OBJ$(N); " APARECE, VINDO D"; LO$(K)
1620 NEXT N
1630 IF T > 30 AND MM(24) = -1 THEN IF INT(RND * 10 + 1) = 7 THEN PRINT VO$; "UM GRITO ESTRANHO...": MM(24) = INT(RND * 15)
1640 IF TM < 1 THEN IF MM(18) = P AND TM = 0 THEN TM = INT(RND * 30 + 1) + 20: PRINT VO$; ": -NAO DEVERIA TER ENTRADO AQUI, MORTAL" ELSE TM = TM - 1: IF TM = 0 THEN TM = TM - 1: PRINT VO$; ": -DEMOREI A CHEGAR, MORTAL, MAS AGORA VOCE PAGARA' POR TER INVADIDO O SANTUARIO DE AKERNAAK": MM(25) = P
1650 IF TE > 0 THEN TE = TE - 1: IF TE = 0 THEN MM(0) = INT(RND * 20)
1660 IF PT > 100 AND MM(26) = -1 THEN IF INT(RND * 10 + 1) = 1 THEN PRINT VO$; ": -VOCE JA' CAUSOU MUITOS PROBLEMAS, VERME.": PRINT "SURGE UM GUERREIRO.": MM(26) = P: GOTO 1560
1670 IF P = MM(19) THEN IF INT(RND * 3 + 1) = 1 THEN PRINT "A ESFERA EXPLODE.": S = -INT(RND * 7 + 1): GOSUB 2110: MM(19) = -1 ELSE IF INT(RND * 4 + 1) = 3 THEN PRINT "A ESFERA BRILHA FORTEMENTE...": P = 19
1680 IF P = MM(16) AND NOT FP(13) THEN IF INT(RND * 7 + 1) = 5 THEN PRINT VO$: PRINT "-EU, A ESTATUA, SIMBOLO DE ANIK, DOU-LHE UM PRESENTE...": MM(13) = P
1690 IF MM(26) = P AND FP(13) AND BL(6) > 0 THEN PRINT VO$: PRINT "-EU, O GUERREIRO DE ANIK, SAUDO O PORTADOR DO CETRO": BL(6) = 0
1700 IF NOT FG OR P <> MM(25) THEN 1720 ELSE PRINT VO$; ":": PRINT "- FRAAAGH ! NYOK ! O CHEIRO DO VINHO "; : IF INT(RND * 4 + 1) = 3 THEN PRINT "ENFURECE O MAGO.": BL(5) = 9 ELSE PRINT "ENFRAQUECE O MAGO.": CF%(5) = CF%(5) - 4
1710 PRINT "ELE QUEBRA SU"; OBJ(12): FP(12) = 0: FG = 0
1720 IF T > 60 AND MM(17) = -1 THEN IF INT(RND * 5 + 1) = 1 THEN MM(17) = INT(RND * 15)
1730 IF PT > 200 AND MM(28) = 18 THEN IF INT(RND * 8 + 1) = 3 THEN PRINT VO$; ":": PRINT "- ATE' AGORA VOCE ENFRENTOU OS APRENDIZES, MEU CARO...": BL(8) = 7: MM(28) = P
1740 IF FP(6) AND MM(28) = P THEN PRINT "SEU ESPELHO DRENA UM POUCO DA MAGIA DO MESTRE": CF%(8) = CF%(8) - 2
1750 IF P = 14 THEN IF INT(RND * 10 + 1) = 8 THEN PRINT "DO ALTO DO MURO SURGE UMA LUZ CINZA...": P = 15: FB = 0
1760 IF MM(17) <> -1 THEN IF INT(RND * 10 + 1) = 6 THEN PRINT VO$; ":": PRINT "- "; OBJ$(17); " ESTA' N"; LO$(MM(17))
1770 IF RN = 0 AND P = 7 THEN IF INT(RND * 20) + 1 = 2 THEN RN = INT(RND * 100 + 1): PRINT "VOCE LE NUMA CARTA DE BARALHO: O NO. MAGICO ê "; RN
1780 IF CF < 0 THEN CF = CF + 1: IF FP(11) THEN IF CF < -1 THEN PRINT "VOCE SE RECUPERA MAIS DEPRESSA, GRAÄAS A SEU ELMO MAGICO": CF = CF + 2
1790 FT = 0
1800 T = T + 1: IF T / 10 = INT(T / 10) THEN PRINT "TEMPO = "; TEMPO; " MINUTO"; : IF TEMPO > 1 THEN PRINT "S" ELSE PRINT
1810 GOTO 300

1811 REM
1812 REM ===== CHEAT !!!
1813 REM
1814 FOR N = 0 TO 15: FP(N) = -1: MM(N) = -1: NEXT N: CF = 32000
1815 PRINT "VOCE INVOCA OS MAGOS DO SETIMO REINO..."
1816 DELAY = 3: GOSUB 60050: CLS
1817 GOTO 300

1820 REM
1830 REM =====LUTA
1840 REM
1850 IF N = 0 THEN 2090 ELSE FB = N: IF FT THEN PRINT VE$; " TOCANDO A FLAUTA": GOTO 1880 ELSE PRINT "VOCE O ATACA COM "; : IF FP(0) THEN PRINT "SUA ESPADA"; : CB = CB + 2 ELSE PRINT "SOCOS E PONTAPES";
1860 IF FP(1) THEN CB = CB + 1: PRINT " E SE DEFENDE COM SEU ESCUDO" ELSE PRINT
1870 IF FP(3) AND N > 26 THEN PRINT "SUA CAPA O PROTEGE DOS GOLPES MAGICOS MAIS FORTES DO"; OBJ$(N): CB = CB + (N - 26) * 2
1880 ON N - 19 GOTO 1890, 1920, 1930, 1950, 1990, 2010, 2030, 2040, 2060, 2070, 2080
1890 IF INT(RND * 4 + 1) = 3 THEN PRINT "ELE SE ASSUSTA COM SUA FURIA E DESAPARECE EM MEIO A UMA FUMACA VERDE.": FB = 0 ELSE 1910
1900 K = INT(RND * 15): IF K <> MM(20) THEN MM(20) = K: RETURN ELSE 1900
1910 GOTO 2090
1920 GOTO 2090
1930 IF FT THEN PRINT "ELE FICA ENFEITIÄADO COM A EXOTICA MUSICA QUE SAI DE SUA FLAUTA NIDDUN E SE ACALMA.": FB = 0: RETURN
1940 GOTO 2090
1950 IF INT(RND * 4 + 1) <> 2 THEN GOTO 1970
1951 PRINT "ELE GESTICULA E TUDO ESCURECE MOMENTANEAMENTE": IF FP(9) THEN CB = CB - 2:  PRINT "MESMO COM SUA LANTERNA ê DIFICIL ENXERGAR" ELSE CB = CB - 5
1960 GOTO 2090
1970 REM
1980 GOTO 2090
1990 IF INT(RND * 3 + 1) = 1 THEN PRINT "O PITON O ENCARA, TENTANDO ROUBAR SUA ENERGIA": CF = CF - INT(RND * 3 + 1): RETURN
2000 GOTO 2090
2010 IF INT(RND * 5 + 1) = 3 THEN PRINT "ELE FAZ UM GESTO ESTRANHO.": IF FP(0) THEN PRINT "SUA ESPADA DERRETE...": FP(0) = 0: TE = INT(RND * 5 + 1) + 10: RETURN ELSE PRINT "VOCE SENTE TONTURAS...": CB = CB - 2
2020 GOTO 2090
2030 GOTO 2090
2040 IF INT(RND * 4 + 1) = 4 THEN D(P, 0) = -1: D(P, 1) = -1: MM(27) = INT(RND * 15): PRINT VO$; ":":  PRINT "A GARGALHADA DO"; OBJ$(N):  PRINT "- MORTAL, VOCE ME SOLTOU, MAS CONSTRUIU SUA PRISAO...":  PRINT "ELE VAI PARA "; LO$(MM(27)): FB = 0: RETURN
2050 GOTO 2090
2060 GOTO 2090
2070 GOTO 2090
2080 GOTO 2090
2090 S = CF - CF%(N - 20) + CB - T / 50 - (N - 20) * 2 + INT(RND * 5 + 1)
2100 IF S < -7 THEN PRINT "INFELIZMENTE, MEU CARO VOCE MORRE...": GOTO 2270
2110 IF S < -4 THEN PRINT "VOCE FOI ATINGIDO SERIAMENTE": CF = CF - 4: GOTO 2210
2120 IF S < 1 THEN PRINT "VOCE FOI ATINGIDO": CF = CF - 2: GOTO 2210
2130 FB = 0
2140 IF S < 4 THEN PRINT "VOCE O ATINGIU": CF%(N - 20) = CF%(N - 20) - 1: GOTO 2210
2150 IF S < 7 THEN PRINT "VOCE O ATINGIU GRAVEMENTE": CF%(N - 20) = CF%(N - 20) - 3: GOTO 2210
2160 PRINT "VOCE O MATOU": MM(N) = -2: PT = (N - 19) * 20 + PT
2170 IF N = 23 THEN IF INT(RND * 3 + 1) = 1 THEN PRINT "UMA LUZ ESTRANHA SURGE. O ELFO ESTA' RESSUSCITADO.": PRINT "E FURIOSO !!!": CF%(3) = 5: MM(N) = P ELSE PRINT "NO LUGAR DO CORPO DO ELFO APARECE UMA ESFERA": MM(19) = P
2180 IF N = 28 OR N = 29 THEN PRINT "VOCE CAI INCONSCIENTE...": PRINT "AO ACORDAR VOCE PERCEBE QUE NAO ESTA' MAIS N"; LO$(P): PRINT VO$; ":": PRINT "- QUEM OUSA DESAFIAR YDYAK, O DRAGAO, ATACANDO SEUS SERVOS ?": P = 17: MM(30) = 17
2190 IF N <> 30 THEN 2210
2200  PRINT "O DRAGAO SOME NUM VORTICE...":  PRINT "SUA CASA ESTA' LIVRE DA BARREIRA.":  PRINT "VOCE VENCEU !": GOTO 2270
2210 RETURN
2220 EX = (MID$(OBJ$(N), 2, LEN(A)) = A OR MID$(OBJ$(N), 3, LEN(A)) = A) AND (MM(N) = P OR FP(N)): RETURN
2230 INPUT "VOCE QUER DESISTIR DO JOGO"; A: IF A = "S" THEN 2270 ELSE 1560
2240 REM
2250 REM =====FIM DE JOGO=====
2260 REM
2270 PT = PT - FP(4) * 100 - FP(5) * 30 - FP(8) * 15 - FP(13) * 50:  PRINT "O JOGO TERMINOU. VOCE FEZ "; PT; " PONTOS": INPUT "VOCE QUER RECOMECAR"; A: IF A = "S" THEN 60 ELSE CLS : END
2280 A = CHR$(INT(RND * 3 + 1) + 65) + CHR$(61 + INT(RND * 3 + 1) * 4) + CHR$(73 + INT(RND * 5 + 1)) + CHR$(61 + INT(RND * 3 + 1) * 4) + CHR$(75 + INT(RND * 3 + 1)): RETURN
60000 REM
60010 REM =====TEMPORIZADOR=====
60020 REM
60030 TEMPO = TEMPO + 1: RETURN
60050 SLEEP DELAY * 350:RETURN
